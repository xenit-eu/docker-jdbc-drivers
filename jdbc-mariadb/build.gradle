plugins {
    id "eu.xenit.docker" version "5.3.2"
    id "eu.xenit.docker-compose" version "5.3.2"
}
ext {
    mariaDbConnectorVersion = '2.7.2'
}

dockerFile {
    dockerFile = file("Dockerfile")
    dockerBuild {
        repository = 'xenit/jdbc-mariadb'
        tags = [mariaDbConnectorVersion]
    }
}

buildDockerImage {
    buildArgs = ['MARIADB_JDBC_VERSION': project.mariaDbConnectorVersion]
}

task testDockerImage {
   doLast {
        def container = dockerCompose.servicesInfos.get('jdbc-driver').firstContainer
        def exitCode = container.inspection.State.ExitCode
        if (exitCode != 0) {
            throw new GradleException("Docker Image validation failed!")
        }
   }
}

dockerCompose {
    captureContainersOutput = true
    useComposeFiles = ['docker-compose.goss.yml']
    isRequiredBy(testDockerImage)   
}

docker {
    if (System.getenv("DOCKER_USER") != null) {
        registryCredentials {
            username = System.getenv("DOCKER_USER")
            password = System.getenv("DOCKER_PASSWORD")
        }
    } else {
        logger.debug "using default credentials"
    }
}
